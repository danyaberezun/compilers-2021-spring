-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    Read      -> sprintf ("READ")
  | Write     -> sprintf ("WRITE")
  | Binop (s) -> sprintf ("BINOP %s", s)
  | LD    (x) -> sprintf ("LD %s", x)
  | LDA   (x)    -> sprintf ("LDA %s", x)
  | ST    (x) -> sprintf ("ST %s", x)
  | STI          -> "STI"
  | Const (n) -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  | DUP          -> "DUP"
  | DROP         -> "DROP"
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];

  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c, insns) {
  fun applyInstruction (c, insn) {
    case insn of
        Read -> (
          var newWorld = readWorld(c[2]);
          [newWorld[0] : c[0], c[1], newWorld[1]]
        ) |
        Write -> [c[0].tl, c[1], writeWorld (c[0].hd, c[2])] |
        Binop (s) -> [evalBinop (s, c[0].tl.hd, c[0].hd) : c[0].tl.tl, c[1], c[2]] |
        LD (x) -> [c[1] (x) : c[0], c[1], c[2]] |
        LDA (x) -> [Ref (x) : c[0], c[1], c[2]] |
        ST (x) -> [c[0].tl, c[1] <- [x, c[0].hd], c[2]] |
        STI ->
          case c[0] of
            hd : Ref(x) : tl -> [hd : tl, c[1] <- [x, hd], c[2]]
          esac |
        Const (n) -> [n : c[0], c[1], c[2]] |
        DROP -> [c[0].tl, c[1], c[2]] |
        _ -> [c[0], c[1], c[2]]
    esac
  }

  fun applyLabelJumps (c, insns) {
    case insns.hd of
      JMP (l) -> [[c[0], c[1], c[2]], env.fromLabel (l)] |
      CJMP (cond, l) -> (
        if (compare("nz", cond) == 0 && c[0].hd != 0 !! compare("z", cond) == 0 && c[0].hd == 0)
        then [[c[0].tl, c[1], c[2]], env.fromLabel (l)]
        else [[c[0].tl, c[1], c[2]], insns.tl]
        fi
      ) |
      _ -> [[c[0], c[1], c[2]], insns.tl]
    esac
  }

  while size(insns) > 0 do
    case insns of
      _ -> (
        var newData = applyLabelJumps (applyInstruction (c, insns.hd), insns);
        c := newData[0];
        insns := newData[1]
      )
    esac
  od;
  c
}


-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  getBuffer (compileAllExpr (stmt, initCompEnv ())[0])
}


public fun compileAllExpr (stmt, env) {
  case stmt of
    Var (v) -> [singletonBuffer (LD (v)), env] |
    Const (c) -> [singletonBuffer (Const (c)), env] |
    Ref(s) -> [singletonBuffer (LDA (s)), env] |
    Binop (s, e1, e2) ->
      var comp1 = compileAllExpr (e1, env);
      var comp2 = compileAllExpr (e2, comp1[1]);
      [comp1[0] <+> comp2[0] <+> singletonBuffer (Binop (s)), comp2[1]] |
    Assn (s, e) ->
      var comp1 = compileAllExpr(s, env);
      var comp2 = compileAllExpr(e, comp1[1]);
      [comp1[0] <+> comp2[0] <+ STI, comp2[1]] |
    Seq (st1, st2) ->
      var comp1 = compileAllExpr (st1, env);
      var comp2 = compileAllExpr (st2, comp1[1]);
      [comp1[0] <+> comp2[0], comp2[1]] |
    Skip -> [emptyBuffer (), env] |
    Read (s) -> [singletonBuffer (Read) <+> singletonBuffer (ST (s)), env] |
    Write (e) ->
      var comp = compileAllExpr (e, env);
      [comp[0] <+> singletonBuffer (Write), comp[1]] |
    While (c, s) ->
      case env.genLabels(2) of [l1, l2, newEnv] ->
        var newData = compileAllExpr (s, newEnv);
        var comp = compileAllExpr (c, newData[1]);
        [singletonBuffer (JMP (l1)) <+ LABEL (l2) <+> newData[0] <+ LABEL (l1)
          <+> comp[0] <+ CJMP ("nz", l2), comp[1]]
      esac |
    DoWhile(s, c) ->
      case env.genLabels(1) of [l, newEnv] ->
        var newData = compileAllExpr (s, newEnv);
        var comp = compileAllExpr (c, newData[1]);
        [singletonBuffer (LABEL (l)) <+> newData[0] <+> comp[0] <+ CJMP ("nz", l), comp[1]]
      esac |
    If (c, st1, st2) ->
      case env.genLabels(2) of [l1, l2, newEnv] ->
        var comp1 = compileAllExpr (c, newEnv);
        var comp2 = compileAllExpr (st1, comp1[1]);
        var comp3 = compileAllExpr (st2, comp2[1]);
        [comp1[0] <+ CJMP ("z", l1) <+> comp2[0] <+ JMP (l2) <+ LABEL (l1) <+> comp3[0] <+ LABEL (l2), comp3[1]]
      esac |
      Ignore (e) -> compileAllExpr(e, env)
  esac
}