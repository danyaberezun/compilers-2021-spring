-- Stack machine.

import Ref;
import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Parser;
import Lexer;
import Expr;
import Builtins;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  -- Shows a location (a reference to function argument, local or global variable)
  fun showLoc (d) {
    fun varity (f) {
      if f then "var" else "val" fi
    }
    
    case d of
      Arg (i) -> sprintf ("arg[%d]", i)
    | Loc (i) -> sprintf ("loc[%d]", i)
    | Glb (x) -> sprintf ("%s", x)
    esac
  }
  
  case i of
    BINOP  (s)       -> sprintf ("BINOP %s", s)
  | LD     (x)       -> sprintf ("LD %s", showLoc (x))
  | LDA    (x)       -> sprintf ("LDA %s", showLoc (x))
  | ST     (x)       -> sprintf ("ST %s", showLoc (x))
  | STI              -> "STI"
  | SEXP   (s, n)    -> sprintf ("SEXP ""%s"", %d", s, n)
  | CONST  (n)       -> sprintf ("CONST %d", n)
  | LABEL  (s, f)    -> sprintf ("LABEL %s, %d", s, f)
  | JMP    (l)       -> sprintf ("JMP %s", l)
  | CJMP   (c, l)    -> sprintf ("CJMP %s, %s", c, l)
  | CALL   (f, n)    -> sprintf ("CALL %s, %d", f, n)
  | BEGIN  (f, a, l) -> sprintf ("BEGIN %s, %d, %d", f, a, l)
  | GLOBAL (x)       -> sprintf ("GLOBAL %s", x)
  | END              -> sprintf ("END")
  | DUP              -> "DUP"
  | DROP             -> "DROP"
  | STRING (s)       -> sprintf ("STRING ""%s""", s)
  | ARRAY  (n)       -> sprintf ("ARRAY %d", n)
  | STA              -> "STA"
  | ELEM             -> "ELEM"
  | BUILTIN (f, n)   -> sprintf ("BUILTIN %s, %d", f, n)
  | META (m)         -> sprintf ("META %s", m.string)
  | PATT (p)         -> sprintf ("PATT %s", p.string)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}                  -> m
            | LABEL (lab, _) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl              -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, a world and a program,
-- returns a final output
fun eval (env, w, insns) {
  -- Global state maps names of global variables to values
  var globalState = ref (fun (x) {error (sprintf ("name ""%s"" is undefined", x), getLoc (x))});
  
  -- Make a fresh local state: a pair of arrays for arguments and local variables;
  -- takes the numbers of arguments and local variables respectively
  fun makeState (a, l) {
    [initArray (a, fun (_) {0}), initArray (l, fun (_) {0})]
  }

  -- Lookups a location in local/global states
  fun lookup ([args, locs], loc) {
    case loc of
      Arg (i) -> args[i]
    | Loc (i) -> locs[i]
    | Loc (i) -> locs[i]
    | Glb (x) -> deref (globalState) (x)
    esac 
  }

  -- Assigns a value to a location
  fun assign ([args, locs], loc, vl) {
    case loc of
      Arg (i) -> args[i] := vl
    | Loc (i) -> locs[i] := vl
    | Glb (x) -> var g = deref (globalState);
                 globalState ::= fun (y) {if compare (x, y) == 0 then vl else g (y) fi}
    esac
  }

  -- Takes n positions from the list, retursn a pair: the remaining list and the taken
  -- sublist
  fun take (list, n) {
    fun inner (n, acc, list) {
      if n == 0
      then [list, acc]
      else inner (n-1, list.hd : acc, list.tl)
      fi
    }

    inner (n, {}, list)
  }

  fun createLocal (stack, nArgs, nLocals) {
    case take (stack, nArgs) of 
      [newStack, args] -> 
        var state = makeState (nArgs, nLocals);
        foldl(fun (i, arg) {assign (state, Arg (i), arg); i+1}, 0, args);
        [newStack, state]
    esac
  }

  fun evalBuiltinSM(name, nArgs, stack, world) {
    case take (stack, nArgs) of [stack, args] -> 
      case evalBuiltin(substring (name, 1, name.length - 1), args, world) of [v, world] -> [v:stack, world] esac
    esac
  }


  (* Assignment *)
  fun eval (c@[stack, controlStack, state, world], insns) {
    --printf ("%s\n", string (insns));
    case insns of
      {} -> c
    | JMP (l) : _                -> eval (c, fromLabel (env, l))
    | CJMP (cond, l) : insnsTail -> 
        case stack of 
          z:s -> if (compare(cond, "nz") == 0 && z != 0) !! (compare(cond, "z") == 0 && z == 0) 
                 then eval ([s, controlStack, state, world], fromLabel (env, l))
                 else eval ([s, controlStack, state, world], insnsTail)
                 fi
        esac
    | CALL (f, nArgs) : p -> 
      case f[0] of
        '$' -> case evalBuiltinSM (f, nArgs, stack, world) of [stack, world] -> eval ([stack, controlStack, state, world], p) esac
      |  _  -> eval ([stack, [state, p]: controlStack, state, world], fromLabel (env, f))
      esac
    | END : tail -> 
        case controlStack of
          {} -> c
        | [localState, newProgram] : tail -> eval([stack, tail, localState, world], newProgram)
        esac
    | i : insnsTail -> 
        var newConfiguration = 
          case i of
            BINOP (s) -> 
              case stack of 
                y:x:tail  -> [evalOp (s, x, y):tail, controlStack, state, world]
              esac
          | LD    (x) -> [lookup(state, x):stack, controlStack, state, world]
          | LDA   (x) -> [Ref(x):stack, controlStack, state, world]
          | ST    (x) -> case stack of v:tail -> assign (state, x, v); [stack, controlStack, state, world] esac
          -- STI is not used
          --| STI       -> case stack of v:Ref(x):tail -> assign (state, x, v); [v:tail, controlStack, state, world] esac
          | CONST (n) -> [n:stack, controlStack, state, world]
          | LABEL (_, _) -> c
          | DROP      -> case stack of n:tail -> [tail, controlStack, state, world] esac
          | BEGIN (_, nArgs, nLocals) -> 
            case createLocal (stack, nArgs, nLocals) of
              [stack, state] -> [stack, controlStack, state, world]
            esac
          | GLOBAL (x)-> assign (state, Glb (x), 0); c
          | STRING (s)-> [s:stack, controlStack, state, world]
          | ARRAY  (n)-> case take (stack, n) of [newStack, args] -> [listArray (args):newStack, controlStack, state, world] esac
          | STA       -> 
            case stack of v:index:x:tail -> 
              if (compare(index, x) != 0) then x[index] := v else assign (state, x[0] (*unfold Ref*), v) fi;
              [v:tail, controlStack, state, world]
            esac
          | ELEM      -> 
            case stack of 
              index:Sexp(s, arr):tail -> [arr[index]:tail, controlStack, state, world] 
            | index:arr:tail          -> [arr[index]:tail, controlStack, state, world] 
            esac
          | DUP       -> case stack of x:tail -> [x:x:tail, controlStack, state, world] esac
          | SEXP(s, n)-> case take (stack, n) of [newStack, args] -> [Sexp (s, listArray (args)):newStack, controlStack, state, world] esac
          | PATT (p)  -> 
              case p of
                Tag (t, size) ->
                  case stack of
                    Sexp (name, exprs) : stack -> [(compare (name, t) == 0 && length (exprs) == size) : stack, controlStack, state, world]
                  | _                  : stack -> [0 : stack, controlStack, state, world]
                  esac
              | Array (size) ->
                  case stack of
                    arr@#array : stack -> [(length (arr) == size) : stack, controlStack, state, world]
                  | _          : stack -> [0 : stack, controlStack, state, world]
                  esac
              esac
          esac;

        eval(newConfiguration, insnsTail)
    esac
  }
  

  -- printf ("%s\n", showSM (insns));
  
  eval ([{}, {}, makeState (0, 0), w], insns) [3].getOutput
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), createWorld (input), insns)
}

-- Compilation environment: generates labels
fun makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, functions) {
  -- Generates a new label
  fun genLabel () {
    [sprintf ("L%d", nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
  }

  -- Adds a new function 
  fun rememberFun (fLabel, args, body) {
    makeCompEnv (nLabels,
                 scopeDepth,
                 state,
                 nLocals,
                 nArgs,
                 Fun (fLabel, args, body, state) : functions)
  }

  -- Enters a local scope
  fun beginScope () {
    makeCompEnv (nLabels, scopeDepth+1, enterScope (state), nLocals, nArgs, functions)
  }

  -- Leaves a local scope
  fun endScope () {
    makeCompEnv (nLabels, scopeDepth-1, leaveScope (state), nLocals, nArgs, functions)
  }

  -- Adds an argument for the current function
  fun addArg (name) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Arg (nArgs)), nLocals, nArgs+1, functions)
  }

  -- Adds a variable in the scope
  fun addVar (name) {
    if isGlobal ()
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name, true)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals, true)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a value in the scope
  fun addVal (name) {
    if isGlobal ()
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name, false)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals, false)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a binding for a function
  fun addFun (name, fLabel, nargs) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Fun (fLabel, nargs)), nLocals, nArgs, functions)
  }
  
  -- Enters a function
  fun beginFun (state) {
    makeCompEnv (nLabels, 2, enterFunction (state), 0, 0, functions)
  }

  -- Get functions and empty functions list
  fun getFuns () {
    [functions, makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, {})]
  }

  -- Lookups variable
  fun lookupVar (name) {
    case lookup (state, name) of
      Loc (i, true) -> Loc (i)
    | Glb (x, true) -> Glb (x)
    | Arg (i)       -> Arg (i)  -- TODO remove after supporting val's
    | _             -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
    esac
  }

  -- Lookups value
  fun lookupVal (name) {
    case lookup (state, name) of
      Loc (i, _) -> Loc (i)
    | Glb (x, _) -> Glb (x)
    | Arg (i)    -> Arg (i)
    | _          -> error (sprintf ("the name ""%s"" does not designate a value", name), getLoc (name))
    esac
  }
  
  -- Lookups a name of a function
  fun lookupFun (name) {
    case lookup (state, name) of
      x@Fun (_, _) -> x
    | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
    esac
  }

  -- Gets the number of local variables
  fun getLocals () {
    nLocals
  }

  -- Generates an intrinsic function name
  fun genFunLabel (name) {
     if isGlobal ()
     then [sprintf ("L%s", name),             makeCompEnv (nLabels  , scopeDepth, state, nLocals, nArgs, functions)]
     else [sprintf ("L%s_%d", name, nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
     fi 
  }

  -- Checks if the current scope is a global
  fun isGlobal () {
    scopeDepth == 2
  }
  
  [genLabel,
   rememberFun,
   beginScope,
   endScope,
   addArg,
   addVar,
   addVal,
   beginFun,
   getFuns,
   lookupVar,
   lookupVal,
   lookupFun,
   getLocals,
   genFunLabel,
   addFun,
   isGlobal
  ]
}

-- Initialize a new compilation environment
fun initCompEnv () {
  makeCompEnv (0, 0, emptyState (), 0, 0, {})
}

-- Accessor functions
fun genLabel (env) {
  env [0] ()
}

fun rememberFun (env, name, args, body) {
  env [1] (name, args, body)
}

fun beginScope (env) {
  env [2] ()
}

fun endScope (env) {
  env [3] ()
}

fun addArg (env, name) {
  env [4] (name)
}

fun addVar (env, name) {
  env [5] (name)
}

fun addVal (env, name) {
  env [6] (name)
}

fun beginFun (env, state) {
  env [7] (state)
}

fun getFuns (env) {
  env [8] ()
}

fun lookupVar (env, name) {
  env [9] (name)
}

fun lookupVal (env, name) {
  env [10] (name)
}

fun lookupFun (env, name) {
  env [11] (name)
}

fun getLocals (env) {
  env [12] ()
}

fun genFunLabel (env, name) {
  env [13] (name)
}

fun addFun (env, name, fLabel, nargs) {
  env [14] (name, fLabel, nargs)
}

fun isGlobal (env) {
  env [15] ()
}

-- Helper function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case genLabel (env) of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Helper function: adds a bunch of arguments
fun addArgs (env, names) {
  foldl (fun (env, name) {addArg (env, name)}, env, names)
}

-- Helper function: adds a bunch of local variables
fun addVars (env, names) {
  foldl (fun (env, name) {addVar (env, name)}, env, names)
}

-- Helper function: adds a bunch of locals values
fun addVals (env, names) {
  foldl (fun (env, name) {addVal (env, name)}, env, names)
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.


-- Some guidelines for compiling functions and scopes.
--
-- 1. use env.beginScope/env.endScope properly;
-- 2. in each scope traverse the definitions twice:
--    a. first, add all bindings into symbolic state,
--       using end.addVar/env.addFun;
--    b. do not forget to generate GLOBAL instructions for
--       top-level variable declarations;
--    c. on the secord traversal add functions themselves for
--       future code generation (use env.rememberFun).
-- 3. the overall flow for codegeneration:
--    a. generate the code for thetopmost expression (the progrman itself);
--    b. take list of nested functions definitions via env.getFuns and
--       generate the code for each function;
--    c. repeat b. until no function definitions appear.
-- 4. when compiling functions, do not forget to create proper
--    environment (use env.beginFun) and register arguments
--    (use env.addArg).

fun compileDefs (env, defs) {
  foldl (fun ([env, globals, funs], decl) {
    case decl of
      Var (names) -> 
        case addVars (env, names) of
          env -> if (isGlobal (env)) 
                 then [env, names +++ globals, funs] 
                 else [env, globals, funs] 
                 fi
        esac
    | f@Fun (name, args, body) -> 
        case genFunLabel (env, name) of
          [fLabel, env] -> 
            case addFun (env, name, fLabel, size (args)) of
              env -> [env, globals, Fun (fLabel, args, body) : funs] 
            esac
        esac
    esac
  }, [env, {}, {}], defs)
}

public fun compileSM (expr) {
  fun compile(env, expression) {
    case expression of
        Const (value) -> [env, singletonBuffer (CONST (value))]
      | Var (x) -> [env, singletonBuffer (LD (lookupVar (env, x)))]
      | Ref (x) -> [env, singletonBuffer (LDA (lookupVar (env, x))) <+ DUP]
      | Binop (op, left, right) -> 
          case compile (env, left) of
            [env1, leftCode] ->
              case compile (env1, right) of
                [newEnv, rightCode] -> [newEnv, leftCode <+> rightCode <+ BINOP (op)]
              esac
          esac
      | Skip              -> [env, emptyBuffer ()]
      | Assn (l, r)       -> 
          case compile(env, l) of
            [env1, lCode] ->
              case compile (env1, r) of
                [newEnv, rCode] -> [newEnv, lCode <+> rCode <+ STA]
              esac
          esac
      | Seq (stm1, stm2)  -> 
          case compile(env, stm1) of
            [env1, stm1Code] ->
              case compile (env1, stm2) of
                [newEnv, stm2Code] -> [newEnv, stm1Code <+> stm2Code]
              esac
          esac
      | If (expr, ifTrue, ifFalse) -> 
          case genLabels (env, 3) of
            [ifTrueLabel, ifFalseLabel, fiLabel, env1] -> 
              case compile (env1, expr) of
                [env2, exprCode] -> 
                  case compile(env2, ifTrue) of
                    [env3, ifTrueCode] -> 
                      case compile(env3, ifFalse) of 
                        [newEnv, ifFalseCode] -> [newEnv, exprCode <+ CJMP ("z", ifFalseLabel) <+ 
                                                          LABEL (ifTrueLabel, false) <+> ifTrueCode <+ JMP (fiLabel) <+ 
                                                          LABEL (ifFalseLabel, false) <+> ifFalseCode <+ LABEL (fiLabel, false)]
                      esac
                  esac  
              esac
          esac
      | While (expr, body) -> 
          case genLabels (env, 2) of
            [condLabel, odLabel, env1] -> 
              case compile(env1, expr) of
                [env2, exprCode] ->
                  case compile (env2, body) of
                    [newEnv, bodyCode] -> [newEnv, singletonBuffer (LABEL (condLabel, true)) <+> exprCode <+ 
                                                 CJMP ("z", odLabel) <+> bodyCode <+ JMP (condLabel) <+ LABEL (odLabel, false)]
                  esac
              esac
          esac
      | DoWhile (body, expr) -> 
          case genLabel (env) of
            [bodyLabel, env1] -> 
              case compile(env1, body) of
                [env2, bodyCode] ->
                  case compile (env2, expr) of
                    [newEnv, exprCode] -> [newEnv, singletonBuffer (LABEL (bodyLabel, true)) <+> bodyCode <+> 
                                                  exprCode <+ CJMP ("nz", bodyLabel)]
                  esac
              esac
          esac
      | Ignore (expr) -> case compile (env, expr) of [newEnv, code] -> [newEnv, code <+ DROP] esac
      | Scope (defs, expr) -> 
          -- 1. traverse all defs 2. remember funs 3. compile expr
          case compileDefs (beginScope (env), defs) of
            [env, globals, funs] -> 
              var globalCommands = map ( fun (v) { GLOBAL (v) }, globals);
              case foldl(fun (e, f@Fun (fLabel, args, body)) { rememberFun (e, fLabel, args, body) }, env, funs) of
                env -> case compile (env, expr) of [env, code] -> [endScope (env), listBuffer (globalCommands) <+> code] esac
              esac
          esac
      | Call (name, args) ->
          case compileArgs (env, args) of
            [env, argsCode] ->
              case lookupFun (env, name) of
                Fun (fLabel, nArgs) -> [env, argsCode <+ CALL (fLabel, nArgs)]
              esac
          esac
      | String (string) -> [env, singletonBuffer (STRING (string))]
      | Array  (exprs)        -> case compileArgs (env, exprs) of [env, argsCode] -> [env, argsCode <+ ARRAY (size (exprs))] esac
      | Elem (expr, index)    -> case compileArgs (env, {expr, index}) of [env, argsCode] -> [env, argsCode <+ ELEM] esac
      | ElemRef (expr, index) -> case compileArgs (env, {expr, index}) of [env, argsCode] -> [env, argsCode] esac
      | Sexp (name, exprs)    -> case compileArgs (env, exprs) of [env, argsCode] -> [env, argsCode <+ SEXP (name, size (exprs))] esac
      | Case (scrutinee, brs) -> 
          fun labelDepth (depth, label) {
            if depth > 0
            then sprintf("%s_drop_%d", label, depth)
            else label
            fi
          }

          fun genDrops (depth, label) {
            var buf = emptyBuffer (), d = depth;

            while d > 0 do
              buf := buf <+ LABEL (labelDepth (d, label), false) <+ DROP;
              d := d - 1
            od;

            buf
          }

          fun max(a, b) { if (a > b) then a else b fi }

          fun compileAllPatterns(env, ps, nextPatt, depth) {
            foldl(fun ([env, i, acc, maxDepth], p) { 
              case compilePattern (env, p, nextPatt, depth + 1) of
                [env, newDepth, pattCode] -> [env, i + 1, acc <+ DUP <+ CONST (i) <+ ELEM <+> pattCode <+ DROP, max (newDepth, maxDepth)]
              esac
            }, [env, 0, emptyBuffer (), depth], ps)
          }

          fun compilePattern(env, patt, nextPatt, depth) {
            case patt of
              Array (ps)      -> 
                case compileAllPatterns (env, ps, nextPatt, depth) of
                  [env, _, psCode, maxDepth] -> 
                    [env, maxDepth, singletonBuffer(DUP) <+ PATT (Array (size(ps))) <+ CJMP ("z", labelDepth (depth, nextPatt)) <+> psCode]
                esac
            | Sexp (name, ps) -> 
                case compileAllPatterns (env, ps, nextPatt, depth) of
                  [env, _, psCode, maxDepth] -> 
                    [env, maxDepth, singletonBuffer(DUP) <+ PATT (Tag (name, size(ps))) <+ CJMP ("z", labelDepth (depth, nextPatt)) <+> psCode]
                esac
            | Named (_, p)    -> compilePattern (env, p, nextPatt, depth)
            | Const (n)       -> [env, depth, singletonBuffer(DUP) <+ CONST (n) <+ BINOP ("==") <+ CJMP ("z", labelDepth (depth, nextPatt))]
            | All             -> [env, depth, emptyBuffer()]
            esac
          }

          fun compileBranches(env, brs, successL) {
            foldl(fun([env, code], [patt, branch]) { 
              case genLabel (env) of
                [nextPatt, env] ->
                  case compilePattern (env, patt, nextPatt, 0) of
                    [env, maxDepth, pattCode] ->
                      case compile (env, branch) of
                        [env, branchCode] -> [env, code <+> pattCode <+ DROP <+> branchCode <+ JMP (successL) <+> genDrops (maxDepth, nextPatt) <+ LABEL (nextPatt, false)]
                      esac
                  esac
              esac
              }, [env, emptyBuffer()], brs
            )
          }

          case genLabel (env) of
            [successL, env] -> 
              case compileBranches (env, brs, successL) of
                [env, code] -> [env, singletonBuffer (LD (lookupVar (env, scrutinee))) <+> code <+ LABEL (successL, false)]
              esac
          esac
    esac
  }

  fun compileArgs (env, args) {
    foldl(fun([e, code], arg) { 
      case compile (e, arg) of [newEnv, argCode] -> [newEnv, code <+> argCode] esac 
    }, [env, emptyBuffer ()], args)
  }

  fun compileFunctions ([funs, env]) {
    fun compileSingleFun([env, acc], Fun (fLabel, args, body, state)) {
      case compile (addArgs (beginFun (env, state), args), body) of 
        [env, code] -> [env, acc <+> singletonBuffer (LABEL (fLabel, false)) <+ BEGIN (fLabel, args.size, getLocals (env)) <+> code <+ END] 
      esac  
    }

    case foldl(compileSingleFun, [env, emptyBuffer ()], funs) of [env, acc] -> 
      case getFuns (env) of 
        [{}, _]     -> acc 
      | [funs, env] -> acc <+> compileFunctions ([funs, env]) 
      esac
    esac

  }

  var compEnv = initCompEnv ().beginScope.addFun ("read", "$read", 0)
                                         .addFun ("write", "$write", 1)
                                         .addFun ("length", "$length", 1);

  case compile (compEnv, expr) of
    [env, code] -> getBuffer(singletonBuffer (LABEL ("main", false)) <+ BEGIN ("main", 0, getLocals (env)) <+> code <+ END <+> compileFunctions (getFuns (env)))
  esac

}
