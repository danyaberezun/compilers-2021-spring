-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;
import Ref;
import Collection;
import STM;

-- Signals an error; takes an error message and location info
public fun error (msg, loc) {
  failure ("%s at %d:%d\n", msg, loc.fst, loc.snd)
}

-- AST environment
fun createEnv (id) {
  fun freshName () {
    [createEnv (id+1), sprintf ("_tmp%d", id)]
  }

  [freshName]
}

fun emptyEnv () {
  createEnv (0)
}

fun freshName (env) {
  env [0] ()
}

-- Attribute-processing functions
-- Attributes are:
--   Val  --- a plain value (aka "rvalue")
--   Ref  --- a reference to a mutable cell (aka "lvalue")
--   Void --- no value (aka "void"/"unit")
--   Weak --- works like Val, but allows to provide a "default" value for void constructs

-- Checks if a plain value "val" can be used in the context described by
-- the attribute "atr".
fun assertValue (atr, vl, loc) {
  case atr of
    Ref  -> error ("reference expected", loc)
  | Void -> vl => fun (vl) {Ignore (vl)}
  | _    -> vl
  esac
}

-- Checks if a void epxression can be used in the context described by
-- the attribute "atr".
fun assertVoid (atr, vl, loc) {
  case atr of
    Void -> vl
  | Val  -> error ("value expected", loc)
  | Weak -> vl => fun (vl) {Seq (vl, Const (0))}
  | _    -> error ("reference expected", loc)
  esac
}

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

-- A helper function to be used with super-combinator "expr"
fun binop (op) {
  [syntax (pos -s[op]), fun (l, loc, r) {
                          fun (a) {
                            assertValue (a, l (Val) =>> fun (l) {
                                            r (Val) =>  fun (r) {
                                              Binop (op, l, r)
                                            }}, loc)
                          }
                        }
  ]
}

-- Helper parser: parses a (possible empty) list of items separated by ","
fun list0 (item) {
  list0By (item, s(","))
}

-- Helper parser: parses a non-empty list of items separated by ","
fun list (item) {
  listBy (item, s(","))
}

-- Helper AST function: expands a "raw" scope expression, reifying
-- initializers into assignments
fun expandScope (defs, expr) {
  fun expandDefs (defs, expr) {
    foldr (fun ([defs, expr], def) {     
             case def of
                [ident, None]         -> [ident : defs, expr]
             |  [ident, Some (value)] -> [ident : defs, Seq (Ignore (Assn (Ref (ident), value)), expr)]
             esac
           },
           [{}, expr],
           defs)
  }

  fun expandValDefs (defs, expr) {
    foldr (fun ([defs, expr], [ident, value]) {     
             [ident : defs, Seq (Ignore (Assn (Ref(ident), value)), expr)]
           },
           [{}, expr],
           defs)
  }
  
  case 
    foldr (fun ([defs, expr], def) {
             case def of
               f@Fun (_, _, _) -> [f : defs, expr]
             | Val (ds) ->
                 case expandValDefs (ds, expr) of
                   [ds, expr] -> [Val (ds) : defs, expr]
                 esac
             | Var (ds)      ->
                 case expandDefs (ds, expr) of
                   [ds, expr] -> [Var (ds) : defs, expr]
                 esac
             esac
           },
           [{}, expr],
           defs) of
    [defs, expr] -> Scope (defs, expr)
  esac  
}

-- Helper AST function: distributes a scope through an expression
fun distributeScope (expr, exprConstructor) {
  case expr of
    Scope (defs, sexpr) -> Scope (defs, exprConstructor (sexpr))
  | _                   -> exprConstructor (expr)
  esac
}

-- Helper AST function: reifies pattern bindings into scope
-- definitions and assignments; takes an returns lists of
-- pattern-branch pairs
fun reifyPatternBindings (matched, brs) {
  map (
    fun ([pat, br]) {      
      fun collectBindings (path, p) {
        fun collectList (path, ps) {
          case
            foldl (fun ([acc, i], p) {
                     [collectBindings (i : path, p) : acc, i+1]                      
                   },
                   [{}, 0],
                   ps) of
            [acc, _] -> flatten (acc)
          esac            
        }
        
        case p of
          Sexp  (_, ps)   -> collectList (path, ps)
        | Array (ps)      -> collectList (path, ps)
        | Named (name, p) -> [name, path] : collectBindings (path, p)
        |  _              -> {}
        esac
      }

      case collectBindings ({}, pat) of
        {}       -> [pat, br]
      | bindings ->
           [pat,
             expandScope (
               {Val (map (
                      fun ([name, path]) {
                        [name, foldr (fun (p, i) {Elem (p, Const (i))}, Var (matched), path)]
                      },
                      bindings))},
               br
             )]
      esac
    },
    brs
  )
}

fun assertVoidFun(token, loc) {
  fun (attr) { assertVoid(attr, token, loc) }
}

fun assertValueFun(token, loc) {
  fun (attr) { assertValue(attr, token, loc) }
}

var ifParser = memo $ eta syntax(
      loc=pos kIf cond=exprParser kThen body=scopeExprParser tail=ifTailParser {
        fun (attr) {
          cond(Val) =>> fun(cond) {
            body(attr) =>> fun(body) {
              tail(attr) => fun(tail) {
                If(cond, body, tail)
              }
            }
          }
        }
      }
    ),
    ifTailParser = memo $ eta syntax(
      kElif cond=exprParser kThen body=scopeExprParser tail=ifTailParser {
        fun (attr) {
          cond(Val) =>> fun(cond) {
            body(attr) =>> fun(body) {
              tail(attr) => fun(tail) {
                If(cond, body, tail)
              }
            }
          }
        }
      } |
      -kElse scopeExprParser -kFi |
      loc=pos kFi { assertVoidFun(returnST(Skip), loc) }
    );

var
  primaryParser = memo $ eta syntax (
    -- array constant
    loc=pos x=inbr[s("["), list0(syntax (e=exprParser {e(Val)})), s("]")] {
      assertValueFun (chainST (x) => fun (x) {Array (x)}, loc)
    } |
    -- string constant
    loc=pos x=strlit {
      assertValueFun (returnST $ String (x), loc)
    } |
    -- character literal
    loc=pos x=chrlit {
      assertValueFun (returnST $ Const (x), loc)
    } |                               
    -- decimal constant
    loc=pos x=decimal {
      assertValueFun(returnST $ Const (stringInt (x)), loc)
      -- fun (a) {assertValue (a, returnST $ Const (stringInt (x)), loc)}
    } |
    -- identifier
    loc=pos x=lident args=inbr[s("("), list0(syntax(e=exprParser {e(Val)})), s(")")]? {
      fun (a) {
        case args of
          None -> returnST $ case a of
                               Ref  -> Ref (x)
                             | Void -> Ignore (Var (x))
                             | _    -> Var (x)
                             esac
        | Some (args) -> assertValue (a, chainST (args) => fun (args) {Call (x, args)}, loc)
        esac}
    } |
    -- S-expression
    loc=pos x=uident args=inbr[s("("), list0(syntax(e=exprParser {e (Val)})), s(")")]? {
      fun (a) {
        assertValue (a, case args of
                          None        -> returnST $ Sexp (x, {})
                        | Some (args) -> chainST (args) => fun (args) {Sexp (x, args)}
                        esac, loc)}
    } |
    -- Brackets
    inbr[s("("), scopeExprParser, s(")")] |
    -- While
    loc=pos kWhile cond=exprParser kDo body=scopeExprParser kOd {
      assertVoidFun(
        cond(Val) =>> fun(cond) { -- returns plain value
          body(Void) => fun(body) { -- returns monade
            While(cond, body)
          }
        },
        loc
      )
    } |
    -- Skip
    loc=pos kSkip {
      assertVoidFun(returnST(Skip), loc)
    } |
    -- If
    ifParser |
    -- Do
    loc=pos kDo body=scopeExprParser kWhile cond=exprParser kOd {
      -- assertVoidFun(DoWhile(body(Void), cond(Val)), loc)
      assertVoidFun(
        body(Void) =>> fun(body) {
          cond(Val) => fun(cond) {
            case body of Scope(defs, bodyExpr) ->
              Scope(defs, DoWhile(bodyExpr, cond))
            esac
          }
        }, 
        loc
      )
    } |
    -- For
    loc=pos kFor init=scopeExprParser s[","] cond=exprParser s[","] incr=exprParser kDo body=scopeExprParser kOd {
      assertVoidFun(
        chainST({init(Void), body(Void), cond(Val), incr(Void)}) => fun({init, body, cond, incr}) {
          case init of Scope(defs, initExpr) ->
            case body of Scope(defs2, bodyExpr) -> -- To decrease scope nesting
              Scope(defs+++defs2, Seq(initExpr, While(cond, Seq(bodyExpr, incr))))
            esac
          esac
        },
        loc
      )
    } |
    -- Case
    loc=pos kCase scrutinee=exprParser kOf patterns=listBy[
      syntax(p=patternParser s["->"] e=scopeExprParser { 
        fun(attr, state) { [p, case e(attr)(state) of [state, x] -> x esac ] } 
      }), 
      s("|")
    ] kEsac {
      fun(attr) { fun(state) {
        case scrutinee(Val)(state) of [state, scrutinee] ->
          case freshName(state) of [state, varName] ->

              fun addScrutineeDefinition(subExpr) {
                Scope({Var({varName})}, Seq(Ignore(Assn(Ref(varName), scrutinee)), subExpr))
              }

              patterns := map(fun(pattern) {pattern(attr, state)}, patterns);
              [state, addScrutineeDefinition(Case(varName, reifyPatternBindings(varName, patterns)))]

          esac
        esac
      }}
    }
  ),   
  patternParser = memo $ eta syntax (
    -- Number
    x=decimal { Const(stringInt(x)) } |
    -- Wildcard
    s["_"] { Wildcard } |
    -- Array
    loc=pos patterns=inbr[s("["), list0(syntax (patternParser)), s("]")] {
      Array (patterns)
    } |
    -- Sexpr
    x=uident patterns=inbr[s("("), list0(patternParser), s(")")]? {
      case patterns of
          None        -> Sexp (x, {})
        | Some (patterns) -> Sexp (x, patterns)
      esac
    } |
    -- Named pattern
    varName=lident pattern=(-s["@"] patternParser)? { Named(varName, pattern) }
  ),
  basicParser = memo $ eta (
    expr ({[Right, {[s (":="), fun (l, loc, r) {
                                 fun (a) {assertValue (a, l (Ref) =>> fun (l) {
                                                          r (Val) =>  fun (r) {
                                                          Assn (l, r)
                                                          }},
                                                        loc)
                                 }
                               }]}],
           [Left , map (binop, {"!!"})],
           [Left , map (binop, {"&&"})],
           [Nona , map (binop, {"==", "!=", "<", ">", "<=", ">="})],
           [Left , map (binop, {"+", "-"})],
           [Left , map (binop, {"*", "/", "%"})]
          },
          postfixParser)
  ),
  
  postfixParser = memo $ eta syntax (
    loc=pos e=primaryParser ps=(i=inbr[s("["), exprParser, s("]")] {Index (i (Val))})* {
      fun (a) {
        foldl (fun (e, p) {                                                                            
                 case p of                                                                                   
                   Index (i) ->
                    fun (a) {
                      case a of
                        Ref -> e (Val) =>> fun (e) {
                               i       =>  fun (i) {
                                 ElemRef (e, i)
                               }} 
                      | _   -> assertValue (a, e (Val) =>> fun (e) {
                                               i       =>  fun (i) {
                                               Elem (e, i)
                                               }},
                                            loc)
                      esac
                    }
                 esac                                                                            
               }, e, ps) (a)
      }}
  ),
  scopeExprParser = memo $ eta syntax (
    declarations=definitionParser* expression=exprParser? {
      fun (attr) {
        chainST(declarations) =>> fun(declarations) {
          case expression of
            None    -> returnST(expandScope(declarations, Skip)) | -- Put in scope anyway to make further work easier
            Some(e) -> e(attr) => fun(e) {
              expandScope(declarations, e)
            }
          esac
        }
      }
    } 
  ),
  definitionParser = memo $ eta syntax (
    kVar ds=list[syntax (x=lident e=(-s["="] basicParser)? {case e of
                                                        None -> returnST $ [x, None]
                                                      | Some (e) ->
                                                          e (Val) => fun (e) {[x, Some (e)]}
                                                      esac})] s[";"] {
      chainST (ds) => fun (ds) {Var (ds)}
    } |
    kVal ds=list[syntax (x=lident s["="] e=basicParser {e (Val) => fun (e) {[x, e]}})] s[";"] {
      chainST (ds) => fun (ds) {Val (ds)}
    } |  
    kFun name=lident args=inbr[s("("), list0 (lident), s(")")] body=inbr[s("{"), scopeExprParser, s("}")] {
      body (Weak) => fun (body) {Fun (name, args, body)}
    }
  ),
  
  exprParser = memo $ eta syntax (
    basicParser
  | s1=basicParser s[";"] s2=exprParser {
     fun (a) {
       s1 (Void) =>> fun (s1) {
       s2 (a)    =>  fun (s2) {
         Seq (s1, s2)
       }}
     }
   }
  );

-- Public top-level parser
public parse = syntax (s=scopeExprParser {s (Void) (emptyEnv ()) [1]});