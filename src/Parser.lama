-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun binop(lhs, op, rhs) {Binop(op, lhs, rhs)}
fun getBinop(ops) {
  map(fun(op) {
    [ s(op), binop ]
  }, ops)
}

-- Primary expression
var primaryExprParser = memo $ eta syntax (x=decimal {Const (stringInt (x))} |
                                           x=lident  {Var (x)}               |
                                           inbr[s("("), exprParser, s(")")]),
      exprParser       = memo $ eta (expr(
        {
          [ Left, getBinop({"!!"}) ],
          [ Left, getBinop({"&&"}) ],
          [ Nona, getBinop({"==", "!=", "<", ">", "<=", ">="}) ],
          [ Left, getBinop({"+", "-"}) ],
          [ Left, getBinop({"*", "/", "%"}) ]
        },
        primaryExprParser
      ));


---- Parsing ifs
var ifTailParser = memo $ eta syntax(
      kElif e=exprParser kThen s=stmtParser tail=ifTailParser { If(e, s, tail) } |
      -kElse stmtParser -kFi |
      kFi { Skip }
);
var ifParser = memo $ eta syntax(
      kIf e=exprParser kThen s=stmtParser tail=ifTailParser { If(e, s, tail) }
);

var simpleStmtParser = memo $ eta syntax(
      kSkip { Skip } |
      kRead x=inbr[s("("), lident, s(")")] { Read(x) } |
      kWrite e=inbr[s("("), exprParser, s(")")] { Write(e) } |
      x=lident s[":="] e=exprParser { Assn(x, e) } |
      ifParser |
      kWhile e=exprParser kDo s=stmtParser kOd { While(e, s) } |
      kDo s=stmtParser kWhile e=exprParser kOd { DoWhile(s, e) } |
      kFor s1=stmtParser s[","] e=exprParser s[","] s2=stmtParser kDo s3=stmtParser kOd { Seq(s1, While(e, Seq(s3, s2))) }
),
    stmtParser = memo $ eta syntax (
      simpleStmtParser | s1=simpleStmtParser s[";"] s2=stmtParser { Seq(s1, s2) }
);

-- Public top-level parser
public parse = stmtParser;
