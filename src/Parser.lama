-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;
import Ref;
import Collection;

-- Signals an error; takes an error message and location info
public fun error (msg, loc) {
  failure ("%s at %d:%d\n", msg, loc.fst, loc.snd)
}

-- An attribute-processing functions
-- Attributes are:
--   Val  --- a plain value (aka "rvalue")
--   Ref  --- a reference to a mutable cell (aka "lvalue")
--   Void --- no value (aka "void"/"unit")
--   Weak --- works like Val, but allows to provide a "default" value for void constructs

-- Checks if a plain value "val" can be used in the context described by
-- the attribute "atr".
fun assertValue (atr, vl, loc) {
  case atr of
    Ref  -> error ("reference expected", loc)
  | Void -> Ignore (vl)
  | _    -> vl
  esac
}

-- Checks if a void epxression can be used in the context described by
-- the attribute "atr".
fun assertVoid (atr, vl, loc) {
  case atr of
    Void -> vl
  | Val  -> error ("value expected", loc)
  | Weak -> Seq (vl, Const (0))
  | _    -> error ("reference expected", loc)
  esac
}

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

-- A helper function to be used with super-combinator "expr"
fun binop (op) {
  [syntax (pos -s[op]), fun (l, loc, r) {
                          fun (a) {
                            assertValue (a, Binop (op, l (Val), r (Val)), loc)
                          }
                        }
  ]
}

-- Helper parser: parses a (possible empty) list of items separated by ","
fun list0 (item) {
  list0By (item, s(","))
}

-- Helper parser: parses a non-empty list of items separated by ","
fun list (item) {
  listBy (item, s(","))
}

-- Helper AST function: expands a "raw" scope expression, reifying
-- initializers into assignments
fun expandScope (defs, expr) {
  fun expandDefs (defs, expr) {
    foldr (fun ([defs, expr], def) {     
             case def of
                [ident, None]         -> [ident : defs, expr]
             |  [ident, Some (value)] -> [ident : defs, Seq (Ignore (Assn (Ref (ident), value)), expr)]
             esac
           },
           [{}, expr],
           defs)
  }

  case 
    foldr (fun ([defs, expr], def) {
             case def of
               f@Fun (_, _, _) -> [f : defs, expr]
             | Var (ds)      ->
                 case expandDefs (ds, expr) of
                   [ds, expr] -> [Var (ds) : defs, expr]
                 esac
             esac
           },
           [{}, expr],
           defs) of
    [defs, expr] -> Scope (defs, expr)
  esac  
}

-- Helper AST function: distributes a scope through an expression
fun distributeScope (expr, exprConstructor) {
  case expr of
    Scope (defs, sexpr) -> Scope (defs, exprConstructor (sexpr))
  | _                   -> exprConstructor (expr)
  esac
}

fun assertVoidFun(token, loc) {
  fun (attr) { assertVoid(attr, token, loc) }
}

fun assertValueFun(token, loc) {
  fun (attr) { assertValue(attr, token, loc) }
}

var ifParser = memo $ eta syntax(
      loc=pos kIf cond=exprParser kThen body=scopeExprParser tail=ifTailParser {
        fun (attr) {
          If(cond(Val), body(attr), tail(attr))
        }
      }
    ),
    ifTailParser = memo $ eta syntax(
      kElif cond=exprParser kThen body=scopeExprParser tail=ifTailParser {
        fun (attr) {
          If(cond(Val), body(attr), tail(attr))
        }
      } |
      -kElse scopeExprParser -kFi |
      loc=pos kFi { fun (attr) { assertVoid(attr, Skip, loc) } }
    );

var primaryParser    = memo $ eta syntax (
      -- array constant
      loc=pos x=inbr[s("["), list0(syntax (e=exprParser {e(Val)})), s("]")] {
        fun (a) {assertValue (a, Array (x), loc)}
      } |
      -- string constant
      loc=pos x=strlit {fun (a) {assertValue (a, String (x), loc)}} |
      -- S-expression
      loc=pos x=uident args=inbr[s("("), list0(syntax(e=exprParser {e(Val)})), s(")")]? {
        fun (a) {assertValue (a, Sexp (x, case args of
          None        -> {}
          | Some (args) -> args
        esac), loc)}
      } |
      -- Brackets
      inbr[s("("), scopeExprParser, s(")")] |
      -- character literal
      loc=pos x=chrlit {fun (a) {assertValue (a, Const (x), loc)}} |
      -- decimal constant
      loc=pos x=decimal{fun (a) {assertValue (a, Const (stringInt (x)), loc)}} |
      -- identifier and function call
      x=lident args=inbr[s("("), list0(syntax(e=exprParser {e(Val)})), s(")")]? {
        fun (a) {
          case args of
            None -> case a of
                      Ref  -> Ref (x)
                    | Void -> Ignore (Var (x))
                    | _    -> Var (x)
                    esac
          | Some (args) -> assertValue (a, Call (x, args))
          esac
        }} |
      -- Skip
      loc=pos kSkip {
        assertVoidFun(Skip, loc)
      } |
      -- While
      loc=pos kWhile cond=exprParser kDo body=scopeExprParser kOd {
        assertVoidFun(While(cond(Val), body(Void)), loc)
      } |
      -- Do
      loc=pos kDo body=scopeExprParser kWhile cond=exprParser kOd {
        -- assertVoidFun(DoWhile(body(Void), cond(Val)), loc)
        case body(Void) of Scope(defs, bodyExpr) ->
          assertVoidFun(Scope(defs, DoWhile(bodyExpr, cond(Val))), loc)
        esac
      } |
      -- For
      loc=pos kFor init=scopeExprParser s[","] cond=exprParser s[","] incr=exprParser kDo body=scopeExprParser kOd {
        case init(Void) of Scope(defs, initExpr) ->
          case body(Void) of Scope(defs2, bodyExpr) -> -- To decrease scope nesting
            assertVoidFun(Scope(defs+++defs2, Seq(
              initExpr, While(cond(Val), Seq(
                bodyExpr, incr(Void)
              ))
            )))
          esac
        esac
      } |
      -- If
      ifParser
    ),
    -- Binops
    basicParser = memo $ eta (expr ({
      [Right, {[s (":="),
                fun (l, loc, r) {
                  fun (a) {assertValue (a, Assn (l (Ref), r (Val)), loc)}
                }]}],
      [Left , map (binop, {"!!"})],
      [Left , map (binop, {"&&"})],
      [Nona , map (binop, {"==", "!=", "<", ">", "<=", ">="})],
      [Left , map (binop, {"+", "-"})],
      [Left , map (binop, {"*", "/", "%"})]
      },
      postfixParser)),
    -- Array indexing
    postfixParser    = memo $ eta syntax (
      loc=pos e=primaryParser ps=(i=inbr[s("["), exprParser, s("]")] {Index (i (Val))})* {
        fun (a) {foldl (fun (e, p) {
          case p of
            Index (i) ->
              fun (a) {
                case a of
                  Ref -> ElemRef (e (Val), i)
                | _   -> assertValue (a, Elem (e (Val), i), loc)
                esac
              }
          esac
        }, e, ps) (a)
      }}
    ),
    scopeExprParser = memo $ eta syntax (
      declarations=definitionParser* expression=exprParser? {
        fun (attr) {
          -- Example of expandScope correct call:
          -- expandScope({Var({["x", None], ["y", Some(1)]}), Var({["z", None]})}, expression(attr))
          -- printf(declarations.string ++ " " ++ expression.string ++ "\n");
          expandScope(declarations, 
            case expression of
              None    -> Skip |
              Some(e) -> e(attr)  
            esac
          )
        }
      } 
    ),
    definitionParser = memo $ eta syntax (kVar ds=list[syntax (lident (
      s["="] e=basicParser {e (Val)})?)] s[";"] {Var (ds)} |
      kFun name=lident
            args=inbr[s("("), list0 (lident), s(")")]
            body=inbr[s("{"), scopeExprParser, s("}")] {
            Fun (name, args, body (Weak))
      }
    ),
    exprParser = memo $ eta syntax (basicParser | s1=basicParser s[";"] s2=exprParser {fun (a) {Seq (s1 (Void), s2 (a))}});

-- Public top-level parser
public parse = syntax (s=scopeExprParser {s (Void)});