-- Parser 
-- Alhasan Alkhaddour M4138c
import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;
import Ref;
import Collection;

-- Signals an error; takes an error message and location info
public fun error (msg, loc) {
  failure ("%s at %d:%d\n", msg, loc.fst, loc.snd)
}

-- An attribute-processing functions
-- Attributes are:
--   Val  --- a plain value (aka "rvalue")
--   Ref  --- a reference to a mutable cell (aka "lvalue")
--   Void --- no value (aka "void"/"unit")
--   Weak --- works like Val, but allows to provide a "default" value for void constructs

-- Checks if a plain value "val" can be used in the context described by
-- the attribute "atr".
fun assertValue (atr, v, loc) {
  case atr of
    Ref  -> error ("reference expected", loc)
  | Void -> Ignore (v)
  | _    -> v
  esac
}

-- Checks if a void epxression can be used in the context described by
-- the attribute "atr".
fun assertVoid (atr, v, loc) {
  case atr of
    Void -> v
  | Val  -> error ("value expected", loc)
  | Weak -> Seq (v, Const (0))
  | _    -> error ("reference expected", loc)
  esac
}

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

-- A helper function to be used with super-combinator "expr"
fun binop (op) {
  [syntax (pos -s[op]), fun (l, loc, r) {
                          fun (a) {
                            assertValue (a, Binop (op, l (Val), r (Val)), loc)
                          }
                        }
  ]
}

-- Helper parser: parses a (possible empty) list of items separated by ","
fun list0 (item) {
  list0By (item, s(","))
}

-- Helper parser: parses a non-empty list of items separated by ","
fun list (item) {
  listBy (item, s(","))
}

-- Helper AST function: expands a "raw" scope expression, reifying
-- initializers into assignments
fun expandScope (defs, expr) {
  fun expandDefs (defs, expr) {
    foldr (fun ([defs, expr], def) {     
             case def of
                [ident, None]         -> [ident : defs, expr]
             |  [ident, Some (value)] -> [ident : defs, Seq (Ignore (Assn (Ref (ident), value)), expr)]
             esac
           },
           [{}, expr],
           defs)
  }

  case 
    foldr (fun ([defs, expr], def) {
             case def of
               f@Fun (_, _, _) -> [f : defs, expr]
             | Var (ds)      ->
                 case expandDefs (ds, expr) of
                   [ds, expr] -> [Var (ds) : defs, expr]
                 esac
             esac
           },
           [{}, expr],
           defs) of
    [defs, expr] -> Scope (defs, expr)
  esac  
}

-- Helper AST function: distributes a scope through an expression
fun distributeScope (expr, exprConstructor) {
  case expr of
    Scope (defs, sexpr) -> Scope (defs, exprConstructor (sexpr))
  | _                   -> exprConstructor (expr)
  esac
}


var elseStmt 	= memo $ eta syntax (
      	-kElse s2=scopeExpr  		
      | kElif e=exp kThen s1=scopeExpr s2=elseStmt { fun (a) {If (e (Val), s1 (a),s2(a))}} 
);

var primary    = memo $ eta syntax (
      loc=pos x=decimal                                         {fun (a) { assertValue (a, Const (stringInt (x)), loc)}}     
    | inbr [s("("), scopeExpr, s(")")] 
    | loc=pos kSkip 		                                        { fun (a) {assertVoid (a, Skip, loc)}} 
    | loc=pos kRead  x=inbr[s("("), lident, s(")")]             { fun (a) {assertVoid (a, Read (x), loc)}} 
    | loc=pos kWrite e=inbr[s("("), exp, s(")")] 								{ fun (a) {assertVoid (a, Write (e (Val)), loc)}} 


    | loc=pos kWhile e=exp kDo s=scopeExpr kOd   								{ fun (a) {assertVoid (a, While (e (Val), s (Void)), loc)}} 
    | kIf e=exp kThen s1=scopeExpr s2=(elseStmt)? kFi           { fun (a) {If (e (Val), s1 (a), case s2 of
                                                                                                          Some (value) -> value (a)
                                                                                                          |	_ -> assertVoid (a, Skip)
                                                                                                esac)}} 

    | loc=pos kDo s=scopeExpr kWhile e=exp kOd                  { fun (a) {assertVoid(a,
                                                                                      distributeScope(s(Void), fun(a){DoWhile (a, e (Val))}),
                                                                                      loc)}}                                                             
    | loc=pos kFor s1=scopeExpr s[","] e=exp s[","] s2=exp kDo s3=scopeExpr kOd 
            { fun (a) 
                { assertVoid (a, 
                                distributeScope(s1(Void), fun (a) {Seq(a , While(e (Val), Seq (s3 (Void), s2 (Void))))}  ),
                              loc)
                }
            
            }																			
    | x=lident args=inbr[s("("), list0(syntax(e=exp {e(Val)})), s(")")]? 
                                                            {fun (a) { case args of
                                                                        None -> case a of
                                                                          Ref  -> Ref (x)
                                                                        | Void -> Ignore (Var (x))
                                                                        | _    -> Var (x)
                                                                            esac
                                                                      | Some (args) -> assertValue (a, Call (x, args))
                                                                    esac
                                                                      }}      


    ),
  parse_var = memo $ eta syntax (
      x=lident 				  	 { [x, None] } 
   	| x=lident s["="] expr=basic { [x, Some (expr(Val))] }
  ),

  dec = memo $ eta syntax (
      kVar vars= list[parse_var] s[";"] { {Var (vars)} } 
    | kFun f=lident s["("] args=list0[lident] s[")"] s["{"] body=scopeExpr s["}"] { {Fun (f, args, body(Weak))} }
  ),

  	declarations = memo $ eta syntax (
      def=dec defs=declarations {def +++ defs} 
    | dec 
  ),

  basic     = memo $ eta (expr ({[Right, {[s (":="),
                                           fun (l, loc, r) {
                                             fun (a) {assertValue (a, Assn (l (Ref), r (Val)), loc)}
                                           }]}],
                                 [Left , map (binop, {"!!"})],
                                 [Left , map (binop, {"&&"})],
                                 [Nona , map (binop, {"==", "!=", "<", ">", "<=", ">="})],
                                 [Left , map (binop, {"+", "-"})],
                                 [Left , map (binop, {"*", "/", "%"})]
                                },
                                primary)),
  exp       = memo $ eta syntax (basic | s1=basic s[";"] s2=exp {fun (a) {Seq (s1 (Void), s2 (a))}}),
  
  scopeExpr = memo $ eta syntax (
      defs = declarations sexpr=exp {fun (a) {expandScope (defs, sexpr (a))}} 
    | sexpr = exp {fun (a){ Scope ({}, sexpr(a))}} 
  );
    

-- Public top-level parser
public parse = syntax (s=scopeExpr {s (Void)});
