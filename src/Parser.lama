-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;
import Ref;
import Collection;

-- Signals an error; takes an error message and location info
public fun error (msg, loc) {
  failure ("%s at %d:%d\n", msg, loc.fst, loc.snd)
}

-- An attribute-processing functions
-- Attributes are:
--   Val  --- a plain value (aka "rvalue")
--   Ref  --- a reference to a mutable cell (aka "lvalue")
--   Void --- no value (aka "void"/"unit")
--   Weak --- works like Val, but allows to provide a "default" value for void constructs

-- Checks if a plain value "val" can be used in the context described by
-- the attribute "atr".
fun assertValue (atr, v, loc) {
  case atr of
    Ref  -> error ("reference expected", loc)
  | Void -> Ignore (v)
  | _    -> v
  esac
}

-- Checks if a void epxression can be used in the context described by
-- the attribute "atr".
fun assertVoid (atr, v, loc) {
  case atr of
    Void -> v
  | Val  -> error ("value expected", loc)
  | Weak -> Seq (v, Const (0))
  | _    -> error ("reference expected", loc)
  esac
}

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

-- A helper function to be used with super-combinator "expr"
fun binop (op) {
  [syntax (pos -s[op]), fun (l, loc, r) {
    fun (a) {
      assertValue (a, Binop (op, l (Val), r (Val)), loc)
    }
  }]
}

-- Helper parser: parses a (possible empty) list of items separated by ","
fun list0 (item) {
  list0By (item, s(","))
}

-- Helper parser: parses a non-empty list of items separated by ","
fun list (item) {
  listBy (item, s(","))
}

-- Helper AST function: expands a "raw" scope expression, reifying
-- initializers into assignments
fun expandScope (defs, expr) {
  fun expandDefs (defs, expr) {
    foldr (fun ([defs, expr], def) {     
             case def of
                [ident, None]         -> [ident : defs, expr]
             |  [ident, Some (value)] -> [ident : defs, Seq (Ignore (Assn (Ref (ident), value)), expr)]
             esac
           },
           [{}, expr],
           defs)
  }

  case 
    foldr (fun ([defs, expr], def) {
             case def of
               f@Fun (_, _, _) -> [f : defs, expr]
             | Var (ds)      ->
                 case expandDefs (ds, expr) of
                   [ds, expr] -> [Var (ds) : defs, expr]
                 esac
             esac
           },
           [{}, expr],
           defs) of
    [defs, expr] -> Scope (defs, expr)
  esac  
}

-- Helper AST function: distributes a scope through an expression
fun distributeScope (expr, exprConstructor) {
  case expr of
    Scope (defs, sexpr) -> Scope (defs, exprConstructor (sexpr))
  | _                   -> exprConstructor (expr)
  esac
}


fun assertVoidFun(token, loc) {
  fun (attr) { assertVoid(attr, token, loc) }
}

fun assertValueFun(token, loc) {
  fun (attr) { assertValue(attr, token, loc) }
}

var ifParser = memo $ eta syntax(
      loc=pos kIf cond=exprParser kThen body=scopeExprParser tail=ifTailParser { 
        fun (attr) {
          If(cond(Val), body(attr), tail(attr)) 
        }
      }
    ),
    ifTailParser = memo $ eta syntax(
      kElif cond=exprParser kThen body=scopeExprParser tail=ifTailParser { 
        fun (attr) {
          If(cond(Val), body(attr), tail(attr)) 
        }
      } |
      -kElse scopeExprParser -kFi |
      kFi { fun (attr) { Skip } }
    );

var primaryParser = memo $ eta syntax (
      -- decimal constant
      loc=pos x=decimal {
        -- fun (attr) {assertValue (attr, Const (stringInt (x)), loc)}
        assertValueFun(Const(stringInt (x)), loc)
      } |
      -- identifier
      x=lident args=inbr[s("("), list0(syntax(e=exprParser {e(Val)})), s(")")]? {
        fun (attr) {
          case args of
            None -> case attr of
                      Ref  -> Ref (x)
                    | Void -> Ignore (Var (x))
                    | _    -> Var (x)
                    esac
          | Some (args) -> assertValue (attr, Call (x, args))
          esac
      }} |
      -- Brackets
      inbr[s("("), scopeExprParser, s(")")] |
      -- Skip
      loc=pos kSkip { 
        assertVoidFun(Skip, loc)
      } |
      -- Read
      loc=pos kRead x=inbr[s("("), lident, s(")")] {
        assertVoidFun(Read(x), loc)
      } |
      -- Write
      loc=pos kWrite e=inbr[s("("), exprParser, s(")")] { 
        assertVoidFun(Write(e(Val)), loc)
      } |
      -- While
      loc=pos kWhile cond=exprParser kDo body=scopeExprParser kOd { 
        assertVoidFun(While(cond(Val), body(Void)), loc)
      } |
      -- Do
      loc=pos kDo body=scopeExprParser kWhile cond=exprParser kOd {
        -- assertVoidFun(DoWhile(body(Void), cond(Val)), loc)
        case body(Void) of Scope(defs, bodyExpr) ->
          assertVoidFun(Scope(defs, DoWhile(bodyExpr, cond(Val))), loc)
        esac
      } |
      -- For
      loc=pos kFor init=scopeExprParser s[","] cond=exprParser s[","] incr=exprParser kDo body=scopeExprParser kOd { 
        case init(Void) of Scope(defs, initExpr) ->
          case body(Void) of Scope(defs2, bodyExpr) -> -- To decrease scope nesting
            assertVoidFun(Scope(defs+++defs2, Seq(
              initExpr, While(cond(Val), Seq(
                bodyExpr, incr(Void)
              ))
            )))
          esac
        esac
      } |
      -- If
      ifParser
    ),
    declarationParser = memo $ eta syntax (
    -- Variables
    -kVar defs=list[syntax(
          varName=lident { [varName, None] } |
          varName=lident s["="] expr=basicParser { [varName, Some(expr(Val)) ] }
        )] -s[";"]  { 
          Var(defs)
        } |
      -- Functions
      kFun name=lident s["("] args=list0[lident] s[")"] 
        s["{"] body=scopeExprParser s["}"] { Fun(name, args, body(Weak)) }
    ),
    scopeExprParser = memo $ eta syntax (
      declarations=rep0[syntax(declarationParser)] expression=exprParser {
        fun (attr) {
          -- Example of expandScope correct call:
          -- expandScope({Var({["x", None], ["y", Some(1)]}), Var({["z", None]})}, expression(attr))
          -- printf(declarations.string ++ "\n");
          expandScope(declarations, expression(attr))
        }
      } 
    ),
    basicParser = memo $ eta (expr ({
      [Right, {[s (":="), fun (l, loc, r) {
        -- fun (attr) {assertValue (attr, Assn (l (Ref), r (Val)), loc)}
        assertValueFun(Assn (l (Ref), r (Val)), loc)
      }]}],
      [Left , map (binop, {"!!"})],
      [Left , map (binop, {"&&"})],
      [Nona , map (binop, {"==", "!=", "<", ">", "<=", ">="})],
      [Left , map (binop, {"+", "-"})],
      [Left , map (binop, {"*", "/", "%"})]
    }, primaryParser)),
    exprParser = memo $ eta syntax (basicParser | s1=basicParser s[";"] s2=exprParser {fun (a) {Seq (s1 (Void), s2 (a))}});

-- Public top-level parser
public parse = syntax (s=scopeExprParser {s (Void)});
