-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |
fun eval (c, stmt) {
  case c of 
    [state, world] -> case stmt of
      Assn (st, expr) -> [state <- [st, evalExpr (state, expr)], world]
    | Read (st)       -> case readWorld (world) of
        [r1, r2] -> [state <- [st, r1], r2] 
        esac
    | Write (expr)    -> [state, writeWorld (evalExpr (state, expr), world)]
    | Skip            -> c
    | Seq (lhs, rhs)  -> eval (eval (c, lhs), rhs)
    | _               -> failure("[ERROR] Unsupported Stmt:eval match")
    esac
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
