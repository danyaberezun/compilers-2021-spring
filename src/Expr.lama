-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Builtins;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Set     (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        if      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        String  (string)             |
--        Array   (expr list)          |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         |
--        Builtin (string, expr list)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVal (state, name) {
  case state.lookup (name) of
    x@Val (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Val (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, vl] -> [c, vl : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun evalAssn (c, Assn (expr1, expr2)) {
  case evalList (c, {expr1, expr2}) of
    [c@[s, w], {l, r}] -> 
      case l of
        ElemRef (arr, index) -> 
          arr[index] := r;
          [c, r]
      | _ ->
          [[s <- [l, r], w], r]
      esac
  esac
}

fun evalBinop(c, Binop (op, expr1, expr2)) {
  case evalList (c, {expr1, expr2}) of
    [c, {l, r}] -> [c, evalOp(op, l, r)]
  esac
}

fun evalSeq (c, Seq (stmt1, stmt2)) {
  eval (eval (c, stmt1).fst, stmt2) 
}

fun evalIf (conf, If(cond, thenExpr, elseExpr)) {
  case eval (conf, cond) of
    [c, v] -> if v then eval (c, thenExpr) else eval (c, elseExpr) fi
  esac
}

fun evalWhile (conf, wh@While(cond, expr)) {
  case eval (conf, cond) of
    [c, v] -> if v then evalWhile((eval (c, expr)).fst, wh) else [c, Void] fi
  esac
}

fun evalDoWhile (conf, DoWhile(expr, cond)) {
  case eval(conf, expr) of
    [c, _] -> evalWhile(c, While(cond, expr)) 
  esac
}

fun evalIgnore (conf, Ignore (expr)) {
  case eval (conf, expr) of
    [c, _] -> [c, Void]
  esac
}

fun evalScope (c@[st, w], Scope (defs, body)) {
  case defs of
    {} -> eval (c, body)
  | _  ->
      case eval ([foldl(
                fun (st, def) {
                  case def of
                    Var (names)            -> addNames (st, names) 
                  | Fun (name, args, body) -> addFunction (st, name, args, body)
                  esac
                }, enterScope(st), defs),
              w], body) of
        [[st, w], v] -> [[leaveScope(st), w], v]
      esac
  esac
}

fun evalCall(c@[st, w], Call(name, args)) {
  case lookup (st, name) of 
    Fun (argvars, body) ->
      var globalState = st;
      case evalList (c, args) of
        [[st, w], values] ->
          case body of
            External -> 
              var result = evalBuiltin(name, values, w);
              [[st, result.snd], result.fst]
          | _ ->
            case eval ([foldl(fun (st, pair) {
                        addName (st, pair.fst, pair.snd)
                      }, 
                      enterFunction(st), 
                      zip(argvars, values)), 
                      w], body) of
              [[st, w], v] -> 
                [[leaveFunction(globalState, getGlobal(st)), w], v]
            esac
          esac
      esac
  esac
}

fun evalString (c, String (value)) {
  [c, value]
}

fun evalArray (c, Array (explist))
{
  case evalList (c, explist) of
    [c, values] -> [c, listArray (values)] 
  esac
}

fun evalElem (c, Elem (arr, index)) {
  case evalList (c, {arr, index}) of
    [c, {arr, index}] -> [c, arr[index]]
  esac
}

fun evalElemRef (c, ElemRef (arr, index)) {
  case evalList (c, {arr, index}) of
    [c, {arr, index}] -> [c, ElemRef(arr, index)]
  esac
}

fun evalSet (c , Set(name, value)) {
  case eval (c, value) of
    ret@[[state, world], value] -> [[state <- [name, value], world], value]
  esac
}

fun eval (c@[s, w], expr) {
  --printf("%s\n", expr.string);
  case expr of
    Var (name)      -> [c, lookup (s, name)]
  | Val (name)      -> [c, lookup (s, name)]
  | Const (x)       -> [c, x]
  | Ref   (name)    -> [c, name]
  | Skip            -> [c, Void]
  | Binop (_, _, _) -> evalBinop   (c, expr)
  | Assn  (_, _)    -> evalAssn    (c, expr)
  | Seq   (_, _)    -> evalSeq     (c, expr)
  | If (_, _, _)    -> evalIf      (c, expr)
  | While (_, _)    -> evalWhile   (c, expr)
  | DoWhile (_, _)  -> evalDoWhile (c, expr)
  | Ignore (_)      -> evalIgnore  (c, expr)
  | Scope (_, _)    -> evalScope   (c, expr)
  | Call (_, _)     -> evalCall    (c, expr)
  | String (_)      -> evalString  (c, expr)
  | Array (_)       -> evalArray   (c, expr)
  | Elem (_, _)     -> evalElem    (c, expr)
  | ElemRef (_, _)  -> evalElemRef (c, expr)
  | Set (_, _)      -> evalSet     (c, expr)
  | _               -> failure     ("Invalid expression type: %s in configuration %s\n", expr.string, c.string)
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope.addName ("read",   Fun ({}, External))
                                      .addName ("write",  Fun ({"a"}, External))
                                      .addName ("length", Fun ({"a"}, External)), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
