-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Fun;
import Builtins;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Set     (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        if      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        Repeat  (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        String  (string)             |
--        Array   (expr list)          |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         |
--        Builtin (string, expr list)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVal (state, name) {
  case state.lookup (name) of
    x@Val (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Val (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, vl] -> [c, vl : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun initScope(s, d){
  while size(d) > 0 do
    s := case d[0] of 
           Var (x) -> addNames (s, x)
         | Fun (f, args, body) -> addFunction (s, f, args, body)
         esac;
    d := d.tl
  od;
  s
}

fun initFunArgs (s, argsNames, args){
  while size(args) >0 do
    s := addName(s, argsNames[0], args[0]);
    argsNames := argsNames.tl;
    args := args.tl
  od;
  s
}

fun findMatchPattern(s, ps, e){
  ------------------
  fun matchList(s, p, e) {
    case [p, e] of
      [p:pt, e:et] -> 
        case match(s, p, e) of
          Nil -> Nil
        | [s, _] -> matchList (s, pt, et)
        esac
    | [{}, {}] -> [s, 0]
    | _ -> Nil
    esac
  }
  ----------------
  fun match(s, p, e) {
    case [p, e] of
      [Wildcard, _] -> [s, 0]
    | [Named (x1), x2] -> [s <- [x1, x2], 0] 
    | [Decimal (x1), x2] ->if x1==x2 then [s, 0] else Nil fi

    | [NamedPattern (x1, args), x2] ->
        case match(s, args, x2) of
          Nil -> Nil
        | [s, _] -> [s <- [x1, x2], 0]
        esac
    
    | [Sexp (l1, x1), Sexp (l2, x2)] ->
        if compare (l1, l2) != 0 then Nil
        else
          matchList(s, x1, arrayList(x2))
        fi
    | [Array (x1), x2@#array] -> matchList(s, x1, arrayList(x2))
    esac
  }

  ---------------
  ---------------
  var done = true;
  while size(ps) > 0 && done do
    case match(initScope(s, ps[0][0][1]), ps[0][0][0], e) of
      Nil -> ps := ps.tl
    | [ns, _] -> s:=ns; done := false
    esac
  od;
  
  [s, ps[0][1]]

}

fun eval (c@[s, w], expr) {
  case expr of
    Const (z) -> [c, z]
  | Var (x) -> [c, lookup(s, x)]
  | Ref (x) -> [c, x]
  | Binop (op, l, r) -> 
      case evalList (c, {l, r}) of
        [c@[s,w], {l1, r1}] -> [c, binop (op, l1, r1)]
      esac
  | Skip -> [c, Void]
  | Assn (l, r) ->
      case evalList (c, {l, r}) of [c@[s,w], {l,r}] -> 
      case l of
        {x, i} -> x[i] := r; [c, r]
      | _ ->  [[s <- [l, r], w], r]
      esac
      esac 
  | Seq (s1, s2) -> 
      case evalList (c, {s1, s2}) of
        [c, {v1, v2}] -> [c, v2]
      esac
  | Ignore (e) -> [ eval (c, e)[0], Void]

  | While (exp, st) ->
      case eval (c, exp) of
        [c, v] -> if (v != 0) then eval (eval (c, st)[0], expr) else [c, Void] fi
      esac

  | If (e, s1, s2) ->
      case eval (c, e) of
        [c, v] -> if (v != 0) then eval (c, s1) else eval (c, s2) fi 
      esac

  | Scope (defs, expr) ->
      s := enterScope (s);
      s := initScope (s, defs);
      case eval ([s,w], expr) of
        [[s,w], r] -> [[leaveScope (s), w], r]
      esac
  | Call (f, args) ->  
      case lookup (s, f) of Fun (argsNames, expr) ->
      case evalList (c, args) of [[s, w], args] ->
      case initFunArgs(enterFunction (s), argsNames, args) of s ->
      case expr of 
        External -> 
          case evalBuiltin(f, args, w) of 
            [x, w] -> [[leaveFunction(c[0], getGlobal(s)), w] ,x]
          esac
      | _ ->    
          case eval ([s, w], expr) of 
            [[s,w], r] -> [ [leaveFunction(c[0], getGlobal(s)), w], r]
          esac
      esac
      esac
      esac
      esac

  | String (x) -> [c, x] 
  | Elem (e1, e2) ->
      case evalList(c, {e1, e2}) of [c, {e1, e2}] -> 
      case e1 of
        Sexp (l, e1) -> [c, e1[e2]]
      | _ -> [c, e1[e2]]
      esac esac
  | ElemRef (e1, e2) -> evalList(c, {e1, e2}) 

  | Array (list) -> 
      case evalList(c, list) of
        [c, list] -> [c, listArray(list)]
      esac

  | Sexp (s, list) -> 
      case evalList (c, list) of
        [c, list] -> [c, Sexp(s, listArray(list))]
      esac
  
  | Case (e, ps) ->
      case eval (c, e) of [c@[s,w], e] ->
        case findMatchPattern (enterScope(s), ps, e) of
          [s, p] -> eval([s, w], p)
        esac
      esac 

  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope.addName ("read",   Fun ({}, External))
                                      .addName ("write",  Fun ({"a"}, External))
                                      .addName ("length", Fun ({"a"}, External)), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}




public fun binop(bp, x, y){
  case bp of
    "+"  -> x +  y 
  | "-"  -> x -  y 
  | "*"  -> x *  y 
  | "/"  -> x /  y 
  | "%"  -> x %  y 
  | "==" -> x == y 
  | "!=" -> x != y 
  | "<"  -> x <  y 
  | "<=" -> x <= y 
  | ">"  -> x >  y 
  | ">=" -> x >= y 
  | "&&" -> x && y 
  | "!!" -> x !! y 
  esac
}
