-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Builtins;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        if      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        String  (string)             |
--        Array   (expr list)          |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         |
--        Builtin (string, expr list)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVal (state, name) {
  case state.lookup (name) of
    x@Val (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Val (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}

fun addDefinitions (state, deflist) {
	case deflist of
		def : othersdef -> (
			case def of 
					Var (name) -> addDefinitions(addNames(state, name), othersdef)
				|	Fun (name, args, body) -> addDefinitions(addFunction(state, name, args, body), othersdef)
			esac
			
		)
		|	_ -> state
	esac
}

fun addArgs (state, args, values) {
	case [args, values] of
			[arg:otherArgs, value:otherValues] -> (
				var news = addName(state, arg, value);
				addArgs (news, otherArgs, otherValues)
			)
		|	_ -> state
	esac
}

                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, vl] -> [c, vl : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}


fun eval (c@[s, w], expr) {
	case expr of
			Assn (expr1, expr2) -> 
				case eval (c, expr1) of
						[c1, Ref(name)] -> 
							case eval (c1, expr2) of 
								[c2@[st2, w2], value] ->
									[[st2 <- [name, value], w2], value]
							esac
					|	[c1, ElemRef (arr, i)] -> 
							case eval (c1, expr2) of 
								[c2, value] ->
									arr[i] := value;
									[c2, value]
							esac
				esac
		|	Seq (expr1, expr2) -> (
		    var res =  eval (c, expr1);
			eval(res[0], expr2)
		)
		|	Skip -> [c, None]
		|	Read (expr) -> (
			case eval (c, expr) of 
				[c1@[st1, w1], Ref(name)] -> (
					case readWorld(w1) of 
						[value, w2] -> [[st1 <- [name, value], w2], None]
					esac
				)
			esac
		)
		|	Write   (expr) -> (
			case eval (c, expr) of 
				[[st1, w1], value] -> (
					var w2 = writeWorld (value, w1);
					[[st1, w2], None]
				)
			esac
		)
		|	If      (cond, thenExpr, elseExpr) -> (
			case eval (c, cond) of 
					[c1, 0] -> eval (c1, elseExpr)
				|	[c1, _] -> eval (c1, thenExpr)
			esac
		)
		|	While   (cond, body) -> (
			case eval (c, cond) of
					[c1, 0] -> [c1, None]
				|	[c1, _] -> (
						var res = eval (c1, body);
						eval (res[0], While (cond, body))
					)
			esac
		)
		|	DoWhile (body, cond) ->  (
			var res = eval (c, body);
			eval (res[0], While (cond, body))
		)
		|	Var     (name) -> [c, lookup(s, name)]
		|	Ref     (name) -> [c, Ref(name)]
		|	ElemRef (arr, i) -> (
			case eval (c, arr) of
				[c1, arrRes] -> (
					case eval (c1, i) of 
						[newc, iRes] -> [newc, ElemRef (arrRes, iRes)]
					esac
				)
			esac
		)	
		|	Const   (value) -> [c, value]
		|	Binop   (op, expr1, expr2) -> (
			case eval (c, expr1)of 
				[c1, res1] -> (
					case eval (c1, expr2) of
						[c2, res2] -> [c2, evalOp (op, res1, res2)]
					esac
				)
			esac
		)
		|	Scope (deflist, expr) -> (
			var s0 = enterScope (s);
			var s1 = addDefinitions  (s0, deflist);
			var res = eval ([s1, w], expr);
			case res of 
				[newc, resVal] -> (
					case newc of 
						[s2, neww] -> (
							var news = leaveScope (s2);
							[[news, neww], resVal]
						)
					esac
				)
			esac
		)
		|	Ignore  (expr) -> (
				var res = eval(c, expr);
				[res[0], None]
			)
		|	String (x) -> [c, x]
		|	Array (elems) -> (
			case evalList (c, elems) of 
				[newc, elemRes] -> [newc, listArray(elemRes)]
			esac
		)
		|	Elem (arr, i) -> (
			case eval (c, arr) of
				[c1, arrRes] -> (
					case eval (c1, i) of 
						[newc, iRes] -> [newc, arrRes[iRes]]
					esac
				)
			esac
		)
		| Call (funName, args) -> (
			case lookup (s, funName) of 
				Fun (argsNames, body) -> (
					case evalList(c, args) of
						[c1@[s1, w1], argsRes] -> (
							case body of 
								External -> (
										case evalBuiltin (funName, argsRes, w1) of 
											[res, w2] -> [[s1, w2], res]
										esac
								)
								|	_ -> (
									var s2 = enterFunction(s1) . addArgs (argsNames, argsRes);
									case eval ([s2, w1], body) of
										[c3@[s3, w3], fres] -> (
											var s4 = leaveFunction (s, getGlobal(s3));
											[[s4, w3], fres]
										)
									esac
								)
								
							esac
						)
					esac
				)
			esac
		)
	esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope.addName ("read",   Fun ({}, External))
                                      .addName ("write",  Fun ({"a"}, External))
                                      .addName ("length", Fun ({"a"}, External)), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
