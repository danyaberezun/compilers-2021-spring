import List;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Scope   (def list, expr)     |
--        Call    (string, expr list)  |
--        Ignore  (expr)
--
-- A definition is either a local variable definition or a function
-- definition:
--
-- def = Var (string list)                |
--       Fun (string, string list, expr)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun checkVar (state, name) {
  case state.lookup (name) of
    Var (_) -> skip
  | _       -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun checkFun (state, name) {
  case state.lookup (name) of
    Fun (_, _) -> skip
  | _          -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Var (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun initScope(s, d){
--  case d of
--    Var (x) :tl -> initScope(addNames (s, x), tl)
--  | Fun (f, args, body) :tl -> initScope(addFunction (s, f, args, body), tl)
--  | _ -> s
--  esac
  while size(d) > 0 do
    s := case d[0] of 
           Var (x) -> addNames (s, x)
         | Fun (f, args, body) -> addFunction (s, f, args, body)
         esac;
    d := d.tl
  od;
  s
}

fun initFunArgs (s, argsNames, args){
--  case [argsNames, args] of
--    [argName:t1, arg:t2] -> initFunArgs( addName(s, argName, arg), t1, t2 )
--  | [0, 0] -> s
--  esac
  while size(args) >0 do
    s := addName(s, argsNames[0], args[0]);
    argsNames := argsNames.tl;
    args := args.tl
  od;
  s
}

fun eval (c@[s, w], expr) {
case expr of
	Assn (l, r) ->
      case evalList (c, {l, r}) of
        [c@[s,w], {l,r}] -> [[s <- [l, r], w], r]
      esac 
  | Seq (stmt_1, stmt_2) ->
      case eval(c, stmt_1) of
        [c, _] -> eval(c, stmt_2)
      esac 
  | Skip -> [c, Bottom]
  | Read (x) ->
      var n;
      n := readWorld(w);
      [[s <- [eval (c, x)[1], n[0]], n[1]], Void]
  | Write (se) ->
      case eval(c, se) of
        [[s, w], v] -> [[s, writeWorld(v, w)], Bottom]
      esac 
  | If (e, s1, s2) ->
      case eval (c, e) of
        [c, v] -> if (v != 0) then eval (c, s1) else eval (c, s2) fi 
      esac
  | While (cond, body) ->
      case eval(c, cond) of
        [c, 0] -> [c, Bottom] |
        _ -> eval(eval(c, body)[0], expr)
      esac 
  | DoWhile (body, cond) ->
      case eval(eval(c, body)[0], cond) of
        [c, 0] -> [c, Bottom] |
        [c, _] -> eval(c, expr)
      esac 
  | Var (x) -> [c, lookup(s, x)]
  | Ref (x) -> [c, x]
  | Const (v) -> [c, v]
  | Binop (o, e1, e2) ->
      case evalList(c, {e1, e2}) of
        [c, {lhs, rhs}] -> [c, evalOp(o, lhs, rhs)]
      esac 
  | Ignore (se) -> [eval(c, se)[0], Bottom]
  | Scope (defs, expr) ->
      s := enterScope (s);
      s := initScope (s, defs);
      case eval ([s,w], expr) of
        [[s,w], r] -> [[leaveScope (s), w], r]
      esac
  | Call (f, args) ->  
      case lookup (s, f) of Fun (argsNames, expr) ->
        case evalList (c, args) of [[s, w], args] ->
          case initFunArgs(enterFunction (s), argsNames, args) of s ->
            case eval ([s, w], expr) of 
              [[s,w], r] -> [ [leaveFunction(c[0], getGlobal(s)), w], r]
            esac
          esac
        esac
      esac
   esac
}

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState (), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}


public fun binop(bp, x, y){
  case bp of
    "+"  -> x +  y 
  | "-"  -> x -  y 
  | "*"  -> x *  y 
  | "/"  -> x /  y 
  | "%"  -> x %  y 
  | "==" -> x == y 
  | "!=" -> x != y 
  | "<"  -> x <  y 
  | "<=" -> x <= y 
  | ">"  -> x >  y 
  | ">=" -> x >= y 
  | "&&" -> x && y 
  | "!!" -> x !! y 
  esac
} 
